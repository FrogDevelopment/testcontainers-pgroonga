plugins {
    id 'java-library'
    id 'maven'
    id 'io.freefair.lombok' version '5.3.0'
    id 'fr.brouillard.oss.gradle.jgitver' version '0.9.1'
}

group 'com.frog-development.testcontainers'
description = 'testcontainers-pgroonga'

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

dependencies {
    def logbackVersion = '1.2.3'
    def postgresqlVersion = '42.2.18'
    def testcontainersVersion = '1.15.1'
    def springBootVersion = '2.4.1'

    implementation "ch.qos.logback:logback-classic:$logbackVersion"

    implementation "org.postgresql:postgresql:$postgresqlVersion"
    implementation "org.testcontainers:postgresql:$testcontainersVersion"
    implementation "org.testcontainers:junit-jupiter:$testcontainersVersion"

    testImplementation "org.springframework.boot:spring-boot-starter-test:$springBootVersion"
    testImplementation "org.springframework.boot:spring-boot-starter-jdbc:$springBootVersion"
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

jgitver {
    strategy = 'PATTERN'
    versionPattern = '${v}-SNAPSHOT'
    tagVersionPattern = '${v}'
}

test {
    reports.html.enabled = false

    useJUnitPlatform {
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allSource
    classifier 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    classifier 'javadoc'
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

// To specify a license in the pom:
install {
    repositories.mavenInstaller {
        pom.project {
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
        }
    }
}

jar {
    enabled true
}

wrapper {
    gradleVersion = "6.7.1"
    distributionType = Wrapper.DistributionType.ALL
}
