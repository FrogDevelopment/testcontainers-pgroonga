plugins {
    id 'java-library'
    id 'maven-publish'
    id 'io.freefair.lombok' version '4.1.6'
    id 'com.jfrog.artifactory' version '4.13.0'
    id 'fr.brouillard.oss.gradle.jgitver' version '0.9.1'
}

group 'fr.frogdevelopment.testcontainers'
description = 'testcontainers-pgroonga'

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

repositories {
    mavenLocal()
    maven {
        url = "$repoUrl/libs-public"
    }
}

dependencies {
    def logbackVersion = '1.2.3'
    def postgresqlVersion = '42.2.8'
    def testcontainersVersion = '1.12.5'
    def springBootVersion = '2.2.4.RELEASE'


    implementation "ch.qos.logback:logback-classic:$logbackVersion"

    implementation "org.postgresql:postgresql:$postgresqlVersion"
    implementation "org.testcontainers:postgresql:$testcontainersVersion"
    implementation "org.testcontainers:junit-jupiter:$testcontainersVersion"

    testImplementation "org.springframework.boot:spring-boot-starter-test:$springBootVersion"
    testImplementation "org.springframework.boot:spring-boot-starter-jdbc:$springBootVersion"
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

jgitver {
    mavenLike true
}

test {
    reports.html.enabled = false

    useJUnitPlatform {
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId =  'pgroonga'
            from components.java

            artifact sourcesJar
        }
    }
}

jar {
    enabled true
}

artifactoryPublish {
    afterEvaluate {
        clientConfig.publisher.repoKey = version?.endsWith('SNAPSHOT') ? 'libs-snapshot-local' : 'libs-release-local'
    }
    clientConfig.publisher.contextUrl = repoUrl
    clientConfig.publisher.username = repoUsername
    clientConfig.publisher.password = repoPassword
    clientConfig.setIncludeEnvVars(true)

    publications('maven')
}

